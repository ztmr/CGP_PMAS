$!
$! Remote PreciceMail Anti-Spam Responder
$!
$
$_VARS:
$  PERLSETUP = "DISK$SYSTEM:[VMS$COMMON.PERL5_8_6]PERL_SETUP.COM"
$  RPMASPL   = "PMAS_ROOT:[COMMON_EXE]RPMAS.PL"
$  QUEUE     = "PMAS$BATCH"
$  JOBOWNER  = "PMAS"
$  JOBLOG    = "PMAS_LOG:PMAS_IMAP_RESPONDER.LOG"
$  JOBFIL    = F$ENVIRONMENT ("PROCEDURE")
$  JOBNAM    = "PMAS IMAP Responder"
$
$_CMDS:
$  SAY	:== "WRITE SYS$OUTPUT"
$
$_INIT:
$  CALL CLEANUP_SIMILAR_JOBS "''QUEUE'" "''JOBNAM'"
$
$  SAY "Running IMAP Responder..."
$  @'PERLSETUP
$
$_PROC:
$  PERL 'RPMASPL
$
$_REQUE:
$  NOW  = F$TIME ()
$  USER = ""
$  RUNAS = F$EDIT(F$GETJPI ("","USERNAME"),"TRIM")
$  IF RUNAS.EQS."SYSTEM" THEN USER = "/USER=''JOBOWNER'"
$
$  SUBMIT/AFTER="''NOW'+00:00:20"/QUEUE='QUEUE' -
         /LOG='JOBLOG'/NOPRINT/KEEP 'USER -
         /NAME="''JOBNAM'" 'JOBFIL
$
$CLEANUP_SIMILAR_JOBS: SUBROUTINE
$  PQNAM = "''P1'"
$  PJNAM = "''P2'"
$  SAY "PQNAM=''PQNAM'; PJNAM=''PJNAM'"
$  X = F$GETQUI ("")
$  CURRENT_PID = F$GETJPI ("", "PID")
$  QNAM = F$GETQUI ("DISPLAY_QUEUE", "QUEUE_NAME", "''PQNAM'", "WILDCARD")
$  _QLOOP:
$    JNAM = F$GETQUI ("DISPLAY_JOB", "JOB_NAME",,"ALL_JOBS")
$    IF JNAM.EQS."" THEN GOTO _BYE
$    JENT = F$GETQUI ("DISPLAY_JOB", "ENTRY_NUMBER",,"FREEZE_CONTEXT")
$    IF JENT.EQS."" THEN GOTO _BYE
$    JPID = F$GETQUI ("DISPLAY_JOB", "JOB_PID",,"FREEZE_CONTEXT")
$    IF JPID.EQS.CURRENT_PID THEN GOTO _QLOOP
$    JFIL = F$GETQUI ("DISPLAY_FILE", "FILE_SPECIFICATION",,"FREEZE_CONTEXT")
$    IF PJNAM.EQS.JNAM
$    THEN SAY "Removing old job ''JNAM' from ''PQNAM'..."
$         DELETE/LOG/ENTRY='JENT'
$         GOTO _BYE
$    ENDIF
$    GOTO _QLOOP
$
$_BYE:
$  X = F$GETQUI ("")
$  EXIT 1
$ENDSUBROUTINE
$
$_EXIT:
$  EXIT
